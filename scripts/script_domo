#!/usr/bin/env osascript

-- ==================================================================
-- PROGRESS
-- ==================================================================

set progress description to "Domo"
set progress total steps to 2
property progressSteps : 0
on updateProgress(description)
	delay 1
	set progress additional description to description
	set progressSteps to progressSteps + 1
	set progress completed steps to progressSteps
end updateProgress


-- ==================================================================
-- START
-- ==================================================================

updateProgress("Starting apps and processes")

-- Open native apps
set nativeApps to {"Todoist", "HyperTerm", "Google Chrome", "Calendar", "Spotify", "Messages", "Mail", "Dash"}
repeat with nativeApp in nativeApps
	run application nativeApp
end repeat

-- Open web apps
set webApps to {"https://domo.demo.domo.com", "https://domo.okta.com/home/jira_onprem/0oa1moe1d0joaWP4P0x7/3549", "http://localhost:3000"}
repeat with webApp in webApps
	do shell script "open " & webApp
end repeat


-- ==================================================================
-- CONFIGURE
-- ==================================================================

updateProgress("Configuring settings")

-- Open terminal/editor session
do shell script "script_tmux_domo"

-- Run window manager
delay 1
tell application "System Events"
	key code 18 using {command down, option down, control down}
end tell

-- Fullscreen terminal/editor
delay 1
tell application "HyperTerm" to activate
tell application "System Events"
	keystroke "f" using {command down, control down}
end tell

-- Open Dev Tools
delay 1
tell application "Google Chrome" to activate
set devToolsTabs to {1, 3}
repeat with devToolsTab in devToolsTabs
	tell application "Google Chrome" to set active tab index of first window to devToolsTab
	tell application "System Events"
		keystroke "i" using {command down, option down}
	end tell
end repeat
