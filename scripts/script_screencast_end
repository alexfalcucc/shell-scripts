#!/usr/bin/env osascript

-- ==================================================================
-- PROGRESS
-- ==================================================================

set progress description to "Screencast End"
set progress total steps to 3
property progressSteps : 0
on updateProgress(description)
	delay 1
	set progress additional description to description
	set progressSteps to progressSteps + 1
	set progress completed steps to progressSteps
end updateProgress


-- ==================================================================
-- START
-- ==================================================================

updateProgress("Starting apps and processes")

-- Open native apps
set nativeApps to {"Flux"}
repeat with nativeApp in nativeApps
	run application nativeApp
end repeat


-- ==================================================================
-- CONFIGURE
-- ==================================================================

updateProgress("Configuring settings")

-- Reset second monitor's screen resolution back to to best for display
delay 1
tell application "SwitchResX Daemon"
	set current mode of display 2 to mode 1 of display 2
end tell

-- Turn on notifications
delay 1
tell application "System Events"
	tell application process "SystemUIServer"
		try
			key down option
			click menu bar item "Notification Center, Do Not Disturb enabled" of menu bar 2
			key up option
		on error
			key up option
		end try
	end tell
end tell


-- ==================================================================
-- QUIT
-- ==================================================================

updateProgress("Quitting apps and processes")

-- Quit native apps
set nativeApps to {"Google Chrome", "SwitchResX Daemon", "ScreenFlow", "Todoist", "iTerm"}
repeat with nativeApp in nativeApps
	tell application nativeApp to quit
end repeat
